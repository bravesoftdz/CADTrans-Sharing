unit Belts.Narrow;
{ {
  Узкоклиновая ремённая передача
}

interface

uses Classes, GearTypesUnit, Graphics, SysUtils, Dialogs,
  GearClassesUnit, Gost;
// System.Classes, GearTypesUnit, Vcl.Graphics, System.SysUtils, Vcl.Dialogs,
// GearClassesUnit, Gost;

type
  TNarrowVeeBelt = class(TBelt)
  private
    // 1 Константы, определяются в конструкторе
    alfa_min, nyu_max, V_max, f_pr: extended;
    // 1 Поле, хранящее входные данные для коллекции
    FInput: TNarrowVeeInput;
    // 1 Поле, хранящее коллекцию выходных данных
    FGears: TNarrowVeeOutputs;
    // 1 Функция проверки передачи на работоспособность True - работоспособна
    function Check(Gear: TNarrowVeeOutput): Boolean;
    // 1 Функция расчёта одной передачи
    function Calculate(VarD1: extended; VarSection: integer): TNarrowVeeOutput;
    // 1 Функция добавления передачи в коллекцию
    function Add(Gear: TNarrowVeeOutput): integer;
    function GetGears(Index: integer): TNarrowVeeOutput;
    procedure SetGears(Index: integer; const Value: TNarrowVeeOutput);
  protected
    procedure SetD1(const Value: extended); override;
    procedure SetA(const Value: extended); override;
    // 1 Процедура проверки корректности входных данных
    procedure Checking; override;
  public
    // 1 Функция возвращает мин. допуст. диаметр ведущего шкива для текущих параметров
    function MinDiameter: extended; override;
    // 1 Функция выводит тип ремня (номер задан в SectionIndex)
    function Section(SectionIndex: integer): String; override;
    constructor Create(Input: TNarrowVeeInput); overload;
    constructor Create; overload;
    // 1 Метод выводит первый элемент коллекции
    function First: TNarrowVeeOutput;
    // 1  Метод выводит последний элемент коллекции
    function Last: TNarrowVeeOutput;
    // 1 Метод очистки коллекции
    procedure Clear; override;
    // 1  Метод выводит количество элементов в коллекции
    function Count: integer; override;
    // 1 Свойство для вывода входных данных для коллекции
    property Input: TNarrowVeeInput read FInput;
    // 1 Функция генерации коллекции передач
    function Collect: integer;
    // 1 Свойство для работы с коллекцией
    property Gears[Index: integer]: TNarrowVeeOutput read GetGears
      write SetGears; default;
  end;

implementation

{
  ******************************** TNarrowVeeBelt *******************************
}
constructor TNarrowVeeBelt.Create(Input: TNarrowVeeInput);
begin
  // inherited Create;

  FFullName := 'Узкоклиновая ремённая передача';
  FBeltType := btNarrowVee;

  { В режиме отладки будет выводиться дополнительное сообщение с типом ошибки }
{$IFDEF Debug}
  try
{$ENDIF}
    P1 := Input.P1;
    n1 := Input.n1;
    SectionIndex := Input.SectionIndex;
    d1 := Input.d1;
    up := Input.up;
    a := Input.a;
    Cp := Input.Cp;
    Tension := Input.Tension;
{$IFDEF Debug}
  except
    on E: Exception do
      ShowMessage('[Input] Вызвана ошибка ' + E.ClassName + ' с сообщением : ' +
        E.Message);
  end;
{$ENDIF}
  // проверочные константы
  alfa_min := 120; { Минимально допустимый угол обхвата }
  nyu_max := 15; { Максимально допустимое число пробегов }
  V_max := 30; { Максимально допустимая скорость ремня }
  f_pr := 0.73; { Приведенный коэффициент трения }

  SetLength(FGears, 0);

{$IFDEF Debug}
  try
{$ENDIF}
    Collect;
{$IFDEF Debug}
  except
    on E: Exception do
      ShowMessage('[Collect] Вызвана ошибка ' + E.ClassName + ' с сообщением : '
        + E.Message);
  end;
{$ENDIF}
end;

function TNarrowVeeBelt.First: TNarrowVeeOutput;
begin
  if Count > 0 then
    Result := Gears[0]
  else
    raise EListError.Create
      ('[TNarrowVeeBelt.First] Попытка выбрать первую передачу в пустой коллекции');
end;

function TNarrowVeeBelt.GetGears(Index: integer): TNarrowVeeOutput;
begin
  if Index < Count then
    Result := FGears[Index]
  else
    raise EListError.Create
      ('[TNarrowVeeBelt.GetGears] Попытка обратиться к передаче с номером больше максимального');
end;

function TNarrowVeeBelt.Section(SectionIndex: integer): String;
begin
  inherited;

  if SectionIndex <= High(UclinT) then
    Result := UclinT[SectionIndex]
  else
    raise EAccessViolation.Create
      ('[TNarrowVeeBelt.GetSection] Некорректно задан номер сечения (SectionIndex)');
end;

function TNarrowVeeBelt.Last: TNarrowVeeOutput;
begin
  if Count > 0 then
    Result := Gears[High(FGears)]
  else
    raise EListError.Create
      ('[TNarrowVeeBelt.Last] Попытка выбрать последнюю передачу в пустой коллекции');
end;

function TNarrowVeeBelt.MinDiameter: extended;
var
  Tip_ukl: integer;
  // если сечение задано как АВТО, то минимально допустимый тип сечения
  // рассчитается и будет храниться в этой переменной
begin
  if n1 = 0 then
    raise EZeroDivide.CreateFmt
      ('[TNarrowVeeBelt.MinDiameter] Ошибка деления на ноль при расчете минимально допустимого диаметра шкива (D1). Частота вращения ведущего шкива (n1) не может быть равна нулю',
      []);

  Tip_ukl := 0;
  if SectionIndex = 0 then
  begin
    inc(Tip_ukl);

    // условие возможности использования какого-то типа ремня
    while 9550 * P1 / n1 > T1S[Tip_ukl] do
      inc(Tip_ukl);

    if Tip_ukl <= High(UclinT) then
      Result := TArrayD[Round(Array_ukl[Tip_ukl, 10])]
    else
      raise ERangeError.CreateFmt
        ('[TNarrowVeeBelt.MinDiameter] При попытке рассчитать минимально допустимый диаметр шкива (D1) неправильно выбирается сечение (Tip_ukl=%d)',
        [Tip_ukl]);
  end
  else
    // если сечение задано, то минимальный диаметр возьмется из 10 столбца
    // массива Array_ukl (возьмется номер диаметра, а не само значение)
    Result := TArrayD[Round(Array_ukl[SectionIndex, 10])];
end;

procedure TNarrowVeeBelt.SetA(const Value: extended);
begin
  inherited;

end;

procedure TNarrowVeeBelt.SetD1(const Value: extended);
var
  Tip_ukl: integer;
  // даже если сечение задано как АВТО, то минимально допустимый тип сечения
  // рассчитается и будет храниться в этой переменной
begin
  if n1 = 0 then
    raise EZeroDivide.Create
      ('[TNarrowVeeBelt.SetD1] Ошибка деления на ноль при записи диаметра шкива (D1), частота вращения (n1) не должна быть равна нулю');

  Tip_ukl := 0;
  if SectionIndex = 0 then
  begin
    inc(Tip_ukl);
    // условие возможности использования какого-то типа ремня
    while 9550 * P1 / n1 > T1S[Tip_ukl] do
      inc(Tip_ukl);
    if Tip_ukl > High(UclinT) then
      raise ERangeError.CreateFmt
        ('[TNarrowVeeBelt.SetD1] При попытке рассчитать минимально допустимый диаметр шкива (D1) неправильно выбирается сечение (Tip_ukl=%d)',
        [Tip_ukl]);
  end
  else
    Tip_ukl := SectionIndex;

  { Ограничение берётся из TArrayD[Round(Array_ukl[Section,10])], 10 столбец в
    массиве Array_ukl это номер (в массиве TArrayD) минимально допустимого диаметра шкива }

  { TODO : Это условие актуально, если передаточное отношение больше единицы,
    если передаточное отношение меньше единицы должно быть немного другое условие }

  if (Value < MinDiameter) and (Value <> 0) then
    raise ERangeError.CreateFmt
      ('[TNarrowVeeBelt.SetD1] Диаметр шкива (D1) не может быть равен %g, минимально допустимое значение для ремня %s равно %g мм (ГОСТ 20889-88)',
      [Value, UclinT[Tip_ukl], MinDiameter]);

  inherited;
end;

procedure TNarrowVeeBelt.SetGears(Index: integer;
  const Value: TNarrowVeeOutput);
begin
  if Index < Count then
    Gears[Index] := Value
  else
    raise EListError.Create
      ('[TNarrowVeeBelt.SetGears] Попытка записать данные в передачу с номером больше максимального');
end;

function TNarrowVeeBelt.Add(Gear: TNarrowVeeOutput): integer;
begin
  SetLength(FGears, Length(FGears) + 1);
  FGears[High(FGears)] := Gear;
  Result := Length(FGears);
end;

function TNarrowVeeBelt.Calculate(VarD1: extended; VarSection: integer)
  : TNarrowVeeOutput;
Var
  b0, Aa, he, hf, b, h, y0, C_alfa, C_v, Sigma_t, f1, t1, nol, deln: extended;
  Ft, p_dop, Ft_dop: extended;
  Output: TNarrowVeeOutput;
  id2: integer;
  l_max: extended;
  F2, alfa_c, m, qqq, qw: extended;
  { F1 - натяжение ведущей ветви;
    F2 - натяжение ведомой ветви;
    Fb - давление на валы ремня;
    F0 - усилие предварительного натяжения;
    Ft - окружное усилие в передаче }
Const
  f = 0.25;
  // C = 0.99; Скольжение ремня принимается равным 1%
begin
  inherited;

  Output.a := a;
  Output.d1 := VarD1;
  Output.SectionIndex := VarSection;

  { ----------------------- Расчет геометрии передачи -------------------------- }

  try
    if up > 1 then
      Output.d2 := Output.d1 * up
    else
      Output.d2 := Output.d1 / up;
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте диаметра ведомого шкива (d2), передаточное отношение (up) не может быть равно нулю');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте диаметра ведомого шкива (d2)');
  end;

  try
    RoundGOST(TArrayD, 1, 38, Output.d2, Output.d2, id2);
  except
    on E: Exception do
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Не удалось округлить диаметр ведомого шкива (d2) до ближайшего значения из ряда линейных размеров');
  end;

  // Если межосевое расстояние задано Авто, рассчитываем его по диаметрам
  if a = 0 then
    Output.a := 1.5 * (Output.d1 + Output.d2);
  // Далее a никогда не используется, только Output.a

  Output.l := 2 * Output.a + Pi * (Output.d1 + Output.d2) / 2 +
    sqr(Output.d2 - Output.d1) / (4 * Output.a);

  try
    RoundGOST(TArrayL, 1, Round(Array_ukl[Output.SectionIndex, 9]), Output.l,
      Output.l, id2);
  except
    on E: Exception do
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Не удалось округлить длину ремня (L) до ближайшего допустимого значения из массива длин клиновых ремней');
  end;

  try
    Output.a := (Output.l - Pi * (Output.d1 + Output.d2) / 2) / 4 +
      sqrt(sqr(Output.l - Pi * (Output.d1 + Output.d2) / 2) - 2 *
      sqr(Output.d1 - Output.d2)) / 4;
  except
    on E: Exception do
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте межосевого расстояния (a). Возможно, неправильно рассчиталась длина ремня (L)');
  end;

  try
    Output.alfa := 180 - (Output.d2 - Output.d1) * 57 / Output.a;
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте угла обхвата ремня (alfa), уточнённое межосевое расстояние (a) оказалось равно нулю');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте угла обхвата ремня (alfa)');
  end;

  { ----------------------- Расчет кинематических параметров передачи ---------- }


  // u := Output.d2 / (C * Output.d1);
  // n2 := n1 * Output.d1 * C / Output.d2;

  if up > 1 then
  begin
    Output.V := Pi * Output.d1 * n1 / 60000
  end
  else
  begin
    Output.V := Pi * Output.d2 * n1 / 60000;
  end;

  try
    Output.nyu := 1000 * Output.V / Output.l;
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте пробега ремня (nyu), длина ремня (L) оказалась равна нулю');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте пробега ремня (nyu)');
  end;

  { ----------------------- Основной расчёт параметров передачи ---------------- }

  try
    b0 := Array_ukl[Output.SectionIndex, 1];
    b := Array_ukl[Output.SectionIndex, 2];
    { h := Array_ukl[Output.Section, 3];
      y0 := Array_ukl[Output.Section, 4]; в этом расчета не используются }
    Aa := Array_ukl[Output.SectionIndex, 5];

    f1 := Array_ukl[Output.SectionIndex, 11];
    t1 := Array_ukl[Output.SectionIndex, 12];
    he := Array_ukl[Output.SectionIndex, 13];
    hf := Array_ukl[Output.SectionIndex, 14];
  except
    on E: Exception do
      raise EAccessViolation.Create
        ('[TNarrowVeeBelt.Calculate] Подбор значений из таблиц вызвал ошибку, неверно задан номер сечения (SectionIndex)');
  end;

  Output.d_e1 := Output.d1 + 2 * he;
  Output.d_e2 := Output.d2 + 2 * he;
  Output.d_f1 := Output.d1 - 2 * hf;
  Output.d_f2 := Output.d2 - 2 * hf;
  C_alfa := 1.24 * (1 - exp(-Output.alfa / 110));
  C_v := 1 - 0.05 * (0.01 * sqr(Output.V) - 1);

  try
    Ft := 1000 * P1 / Output.V;
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте окружного усилия (Ft), скорость ремня (V) оказалась равна нулю');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте окружного усилия (Ft)');
  end;

  try
    Sigma_t := (27.1 - 2.215 * ln(Output.nyu)) *
      exp(-0.421 * ln(Array_ukl[Output.SectionIndex, 1])) - 49 *
      Array_ukl[Output.SectionIndex, 1] / Output.d_e1 - 1.12E-03 * Output.V
      * Output.V;
  except
    on E: Exception do
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте (Sigma_t)');
  end;

  try
    Output.z := Round(Ft * Cp / (Sigma_t * Output.a * 0.9 * C_alfa *
      C_v) + 0.5);
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте числа ремней (z)');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте числа ремней (z)');
  end;

  Output.Bs := 2 * f1 + t1 * (Output.z - 1);

  { ----------------------- Расчет сил ----------------------------------------- }

  alfa_c := 0.7 * Output.alfa * Pi / 180;
  m := exp(f_pr * alfa_c);

  // ниже нет проверок по m, потому что она гарантированно больше нуля
  f1 := m / (m - 1) * Ft;
  F2 := f1 - Ft;
  Output.F0 := Ft / 2 * ((m + 1) / (m - 1));

  try
    Output.Fb := sqrt(sqr(f1) + sqr(F2) - 2 * f1 * F2 *
      cos((180 - Output.alfa) * Pi / 180));
  except
    on E: EInvalidOp do
      raise EInvalidOp.Create
        ('[TNarrowVeeBelt.Calculate] Взят корень из отрицательного значения при расчёте давления на валы ремня (Fb)');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте давления на валы ремня (Fb)');
  end;

  Output.Vol := (Output.Bs * (Pi * (sqr(Output.d1) + sqr(Output.d2)) + 4 *
    Output.a * (Output.d1 + Output.d2))) / 8 * 1E-6;

  alfa_c := 0.7 * Output.alfa * Pi / 180;
  qw := exp(f * alfa_c);

  try
    qqq := 0.35 * qw * Ft / ((qw - 1) * Aa) + 7.5 * exp(1.57 * ln(b0)) /
      Output.d_e1 + 1.27E-3 * Output.V * Output.V;
    Output.Lh := 283 * Output.l / (Output.V * qqq) + 250;
    if Output.Lh > 6000 then
      Output.Lh := exp(0.8 * ln(Output.Lh));
  except
    on E: EZeroDivide do
      raise EZeroDivide.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка деления на ноль при расчёте долговечности (Lh)');
    on E: EInvalidOp do
      raise EInvalidOp.Create
        ('[TNarrowVeeBelt.Calculate] Под логарифмом оказалось отрицательное или нулевое значение при расчёте долговечности (Lh)');
    else
      raise Exception.Create
        ('[TNarrowVeeBelt.Calculate] Ошибка при расчёте долговечности (Lh)');
  end;

  p_dop := b0 * C_alfa * C_v * Cp;
  Ft_dop := Output.z * b * p_dop;
  Output.PMax := Ft_dop * Output.V / 1000;

  Result := Output;
end;

function TNarrowVeeBelt.Check(Gear: TNarrowVeeOutput): Boolean;
begin
  Result := True;

  if Gear.V > V_max then
    Result := False;

  if (Gear.z < 1) or (Gear.z > 11) then
    Result := False;

  if Gear.alfa < alfa_min then
    Result := False;

  if Gear.nyu > nyu_max then
    Result := False;
end;

procedure TNarrowVeeBelt.Checking;
begin
  inherited;
end;

procedure TNarrowVeeBelt.Clear;
begin
  if Count <> 0 then
    SetLength(FGears, 0);
end;

function TNarrowVeeBelt.Collect: integer;
var
  CurrGear: TNarrowVeeOutput;
  d1x: extended;
  SecIndex, id: integer;
begin
  Clear;
  Result := 0;

  if (SectionIndex <> 0) and (d1 <> 0) then
  begin
    if 9550 * P1 / n1 <= T1S[SectionIndex] then
    begin
      CurrGear := Calculate(d1, SectionIndex);
      if Check(CurrGear) then
        Result := Add(CurrGear);
    end
  end;

  if (SectionIndex = 0) and (d1 <> 0) then
  begin
    for SecIndex := High(UclinT) downto Low(UclinT) + 1 do
    begin
      if 9550 * P1 / n1 <= T1S[SecIndex] then
      begin
        CurrGear := Calculate(d1, SecIndex);
        if Check(CurrGear) then
          Result := Add(CurrGear);
      end;
    end;
  end;

  if (SectionIndex <> 0) and (d1 = 0) then
  begin
    if 9550 * P1 / n1 <= T1S[SectionIndex] then
    begin
      d1x := TArrayD[Round(Array_ukl[SectionIndex, 10])];
      repeat
        CurrGear := Calculate(d1x, SectionIndex);
        d1x := 1.25 * d1x;
        RoundGOST(TArrayD, 1, 38, d1x, d1x, id);
        if Check(CurrGear) then
        begin
          Result := Add(CurrGear);
        end;
      until CurrGear.l >= TArrayL[Round(Array_ukl[SectionIndex, 9])];
    end;
  end;

  if (SectionIndex = 0) and (d1 = 0) then
  begin
    for SecIndex := High(UclinT) downto Low(UclinT) + 1 do
    begin
      if 9550 * P1 / n1 <= T1S[SecIndex] then
      begin
        d1x := TArrayD[Round(Array_ukl[SecIndex, 10])];
        repeat
          CurrGear := Calculate(d1x, SecIndex);
          d1x := 1.25 * d1x;
          RoundGOST(TArrayD, 1, 38, d1x, d1x, id);
          if Check(CurrGear) then
          begin
            Result := Add(CurrGear);
          end;
        until CurrGear.l >= TArrayL[Round(Array_ukl[SecIndex, 9])];
      end;
    end;
  end;

end;

function TNarrowVeeBelt.Count: integer;
begin
  Result := Length(FGears);
end;

constructor TNarrowVeeBelt.Create;
var
  NB: TNarrowVeeInput;
begin
  NB.P1 := 50;
  NB.n1 := 1000;
  NB.up := 2;
  NB.d1 := 140;
  NB.a := 0;
  NB.Q := 1;
  NB.Cp := 1;
  NB.Tension := ttAutomatic;
  NB.SectionIndex := 0;

  Create(NB);
end;

end.
